apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao' // apply plugin
apply plugin: 'kotlinx-serialization'


def keystorePropertiesFile = rootProject.file("buildsystem/keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    signingConfigs {

        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.carlos.grabredenvelope"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 5
        versionName "1.0.4"
        flavorDimensions "5" //和versionCode相同
        ndk {
            //选择要添加的对应 cpu 类型的 .so 库。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : localProperties.getProperty('JPUSH_APPKEY'), //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
        debug {
            signingConfig signingConfigs.release

        }
    }

    //多渠道打包
    productFlavors {
        dev {          //开发测试版本
            manifestPlaceholders = [
                    UMENG_APPKEY: localProperties.getProperty('UMENG_APPKEY_DEV'),
            ]
        }
        production {   //正式发布版本
            manifestPlaceholders = [
                    UMENG_APPKEY: localProperties.getProperty('UMENG_APPKEY'),
            ]
        }
    }

}

//修改APK名字
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "QiangHongBao_${versionName}.apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    implementation 'cn.jiguang.sdk:jpush:3.1.8'
    implementation 'cn.jiguang.sdk:jcore:1.2.7'
    implementation 'com.github.xbdcc:Cutils:0.0.8'
    implementation 'com.umeng.umsdk:analytics:8.0.0'
    implementation 'com.umeng.umsdk:common:2.0.0'
    implementation 'com.tencent.bugly:crashreport:2.8.6'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'

}
repositories {
    mavenCentral()
}
